plugins {
    id 'org.springframework.boot' version "$springBootVersion"
    id 'io.spring.dependency-management' version "$springDependencyManagement"
    id 'java'
    id 'jacoco'
}

repositories {
    maven { url 'https://repo.spring.io/milestone' }
    mavenCentral()
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'org.springframework.boot'

    compileJava.options.encoding = 'UTF-8'
    compileTestJava.options.encoding = 'UTF-8'
    sourceCompatibility = JavaVersion.VERSION_18
    targetCompatibility = JavaVersion.VERSION_18

    repositories {
        maven { url 'https://repo.spring.io/milestone' }
        mavenCentral()
    }

    configurations {
        compileOnly.extendsFrom annotationProcessor
        testCompileOnly.extendsFrom annotationProcessor
    }

    dependencies {
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        testCompileOnly 'org.projectlombok:lombok'
        testAnnotationProcessor 'org.projectlombok:lombok'
    }

    // tests configuration
    tasks.named('test') {
        useJUnitPlatform()
        finalizedBy jacocoTestReport
    }
    tasks.named('jacocoTestReport') {
        dependsOn test
    }

    // enable preview features
    final PREVIEW_OPTION = '--enable-preview'
    tasks.withType(JavaCompile).each {
        it.options.compilerArgs.add(PREVIEW_OPTION)
    }
    tasks.withType(JavaExec).each {
        it.jvmArgs([PREVIEW_OPTION])
    }
    tasks.withType(Test).each {
        it.jvmArgs([PREVIEW_OPTION])
    }

    // JVM memory tuning
    tasks.withType(JavaExec).each {
        it.jvmArgs([
                '-XX:+HeapDumpOnOutOfMemoryError',
                '-XX:MaxMetaspaceSize=128m',
                '-XX:+UseShenandoahGC',
                '-Xmx512m',
        ])
    }
}
